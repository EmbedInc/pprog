;   ***************************************************************
;   * Copyright (C) 2008, Embed Inc (http://www.embedinc.com)     *
;   *                                                             *
;   * Permission to copy this file is granted as long as this     *
;   * copyright notice is included in its entirety at the         *
;   * beginning of the file, whether the file is copied in whole  *
;   * or in part and regardless of whether other information is   *
;   * added to the copy.                                          *
;   *                                                             *
;   * The contents of this file may be used in any way,           *
;   * commercial or otherwise.  This file is provided "as is",    *
;   * and Embed Inc makes no claims of suitability for a          *
;   * particular purpose nor assumes any liability resulting from *
;   * its use.                                                    *
;   ***************************************************************
;
;   Low level procedural interface to the EEPROM.
;
;   The following routines are exported which is the rest of the system's
;   sole access to the EERPOM:
;
;     PROM_ADR  -  Set address of the next EEPROM read or write from REG2:REG1.
;
;     PROM_READ  -  Read byte from current EEPROM address into REG0,
;       then increment the current address.
;
;     PROM_WRITE  -  Write REG0 to current EEPROM address, then
;       increment the current address.
;
;     PROM_FLUSH  -  Guarantees all write data is physically written
;       to the EEPROM.  Write data may be buffered until this call.
;
;   This version is for the internal data EEPROM of a PIC 18.
;
;   FLAG_PROM should be used by application routines to guarantee non-overlapped
;   access to the EEPROM by multiple tasks.  A task should not call any of the
;   routines above until FLAG_PROM is clear, set the flag so other tasks will
;   not access the EEPROM, perform the EEPROM operation, then clear the flag.
;

;*******************************************************************************
;
;   Local state.
;
         defram  lbankadr

adr0     res     1           ;EEPROM address of next transfer, low byte
  ifdef eeadrh
adr1     res     1           ;high byte if hardware has high byte adr register
    endif

.prom    code
;
;*******************************************************************************
;
;   Macro WAIT_WRITE
;
;   Wait for the current write cycle, if any, to complete.  This macro
;   also guarantees that access to the data EEPROM, as apposed to program
;   memory, is selected.
;
wait_write macro
         local   loop
         dbankif eecon1
loop                         ;back here until write operation completed
         btfsc   eecon1, wr  ;no write operation in progress ?
         goto    loop        ;write in progress, back and check again
         endm
;
;*******************************************************************************
;
;   Subroutine PROM_INIT
;
;   Initialize the hardware and state managed by this module.
;
         glbsub  prom_init, noregs

         dbankif lbankadr
         clrf    adr0        ;init EEPROM address for next transfer
  ifdef adr1
         clrf    adr1
    endif

         leaverest
;
;*******************************************************************************
;
;   Subroutine PROM_ADR
;
;   Set the address for subsequent EEPROM accesses.  The new address
;   is passed in REG2,REG1.
;
         glbsub  prom_adr, noregs

         movff   reg1, adr0  ;set address low byte
  ifdef adr1
         movff   reg2, adr1  ;set address high byte
    endif

         leaverest
;
;*******************************************************************************
;
;   Subroutine PROM_READ
;
;   Read one byte from the EEPROM into REG0.  The EEPROM address is advanced
;   by 1 after the read.
;
         glbsub  prom_read, noregs

         wait_write          ;wait for previous write, if any, to complete
;
;   Set the EEPROM address.
;
         movff   adr0, eeadr ;set the address low byte
  ifdef adr1
         movff   adr1, eeadrh ;set address high byte
    endif
;
;   Have the EEPROM perform a read.  The result is written into the EEDATA
;   register by the hardware.
;
         dbankif eecon1
         clrf    eecon1      ;set up for access to the data EEPROM
         bsf     eecon1, rd  ;read from the current EEPROM location
;
;   Increment the current EEPROM address.
;
         dbankif lbankadr
         incf    adr0        ;increment low address byte
  ifdef adr1
         skip_nz             ;no carry into high byte ?
         incf    adr1        ;propagate the carry
    endif
;
;   Pass back the byte read from the EEPROM.
;
         movff   eedata, reg0 ;return the data byte in REG0

         leaverest
;
;*******************************************************************************
;
;   Subroutine PROM_WRITE
;
;   Write the byte in REG0 to the EEPROM.  The EEPROM address is advanced
;   by 1 after the write.
;
         glbsub  prom_write, regf1

         wait_write          ;wait for previous write, if any, to complete
;
;   Set the EEPROM address.
;
         movff   adr0, eeadr ;set the address low byte
  ifdef adr1
         movff   adr1, eeadrh ;set address high byte
    endif
;
;   Set the EEPROM data to write.
;
         movff   reg0, eedata
;
;   Perform special sequence to enable writing to the EEPROM.  Yes, this
;   is a bit strange, but that's what is required.
;
         dbankif eecon1
         clrf    eecon1      ;set up for access to the data EEPROM
         bsf     eecon1, wren ;set bit to enable writes to EEPROM

         movff   intcon, reg1 ;save byte containing interrupt enable/disable status

         dbankif eecon2
         movlw   h'55'       ;get first magic value to write to EECON2
         bcf     intcon, gie ;make sure interrupts are off
         movwf   eecon2      ;write first magic value to EECON2
         movlw   h'AA'       ;get the second magic value
         movwf   eecon2      ;write the second magic value to EECON2
         dbankif eecon1
         bsf     eecon1, wr  ;start the write cycle

         btfsc   reg1, gie   ;interrupts were originally disabled ?
         bsf     intcon, gie ;were originally enabled, re-enable them

         dbankif eecon1
         bcf     eecon1, wren ;disable any new write cycles
;
;   Increment the current EEPROM address.
;
         dbankif lbankadr
         incf    adr0        ;increment low address byte
  ifdef adr1
         skip_nz             ;no carry into high byte ?
         incf    adr1        ;propagate the carry
    endif

         leaverest
;
;*******************************************************************************
;
;   Subroutine PROM_FLUSH
;
;   Force all buffered write data, if any, to be physically written to the
;   non-volatile memory.  Write data is sometimes buffered and then written
;   all at once within an EEPROM page.  This call causes any such buffered
;   write data to be physically written.
;
;   This implementation is for the internal EEPROM of a PIC 18.  Each
;   byte is written individually to this EEPROM and not buffered.  This
;   version will only wait for the current write cycle, if any, to
;   complete.
;
         glbsub  prom_flush, noregs

         wait_write          ;wait for previous write, if any, to complete

         leaverest
